plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.21' apply false
    id 'org.jlleitschuh.gradle.ktlint' version '11.0.0'
    id 'maven-publish'
}

allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects { subproject ->
    publishing {
        publications {
            def publicationName = subproject.name.toString().replace("-", "")
            "$publicationName"(MavenPublication) {
                groupId project.property('publication.groupId')
                artifactId subproject.name
                version project.property('version')

                // Include your library's AAR file
                artifact("$rootDir/${subproject.name}/build/outputs/aar/${subproject.name}-release.aar")

                pom {
                    licenses {
                        license {
                            name = project.property('pom.license.name')
                            url = project.property('pom.license.url')
                            distribution = project.property('pom.license.distribution')
                        }
                    }
                    developers {
                        developer {
                            id = project.property('pom.developer.id')
                            name = project.property('pom.developer.name')
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = project.property('nexus.url')
                credentials {
                    username = project.property('nexus.username')
                    password = project.property('nexus.password')
                }
            }
        }
    }
}
