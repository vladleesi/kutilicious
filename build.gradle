plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.21' apply false
    id 'org.jlleitschuh.gradle.ktlint' version '11.0.0'
    id 'maven-publish'
    id 'signing'
}

allprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
}

signing {
    sign publishing.publications
}

subprojects { subproject ->
    publishing {
        def subprojectName = subproject.name.toString()
        publications {
            "$subprojectName"(MavenPublication) {
                groupId project.property('publication.groupId')
                artifactId subproject.name
                version project.property('publication.version')

                // Include your library's AAR file
                artifact("$rootDir/$subprojectName/build/outputs/aar/$subprojectName-release.aar")

                pom {
                    url = project.property('pom.url')
                    name = project.property('pom.name')
                    description = project.property('pom.description')
                    scm {
                        url = project.property('pom.scm.url')
                        connection = project.property('pom.scm.connection')
                        developerConnection = project.property('pom.scm.developer.connection')
                    }
                    licenses {
                        license {
                            name = project.property('pom.license.name')
                            url = project.property('pom.license.url')
                            distribution = project.property('pom.license.distribution')
                        }
                    }
                    developers {
                        developer {
                            id = project.property('pom.developer.id')
                            name = project.property('pom.developer.name')
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = project.property('nexus.url')
                credentials {
                    username = project.property('nexus.username')
                    password = project.property('nexus.password')
                }
            }
        }
    }
}
